defmodule QyCore.Recipe do
  @moduledoc """
  定义一个处理流程（菜谱）。
  """

  alias QyCore.Recipe.Step
  import QyCore.Utilities, only: [ensure_full_step: 1]

  @type t :: %__MODULE__{
          steps: [Step.t()],
          name: atom() | nil,
          opts: keyword()
        }
  defstruct steps: [], name: nil, opts: []

  @spec new([Step.t()], keyword()) :: t()
  def new(steps, opts \\ []) do
    # TODO: 这里可以做更多的验证和预处理
    %__MODULE__{
      steps: steps,
      name: Keyword.get(opts, :name),
      opts: opts
    }
  end

  @doc """
  全局注入选项 (支持深度注入)。

  ### selector 的选项

  * 模块或函数本体 => 匹配就可以
  * 检查函数 => 输入 step ，自定义具体逻辑
  """
  @spec assign_options(
          QyCore.Recipe.t(),
          Step.implementation() | (Step.t() -> boolean()),
          keyword() | %{}
        ) :: QyCore.Recipe.t()
  def assign_options(%__MODULE__{} = recipe, selector, new_opts) do
    walk(recipe, fn step ->
      if do_match(step, selector) do
        Step.inject_options(ensure_full_step(step), new_opts)
      else
        step
      end
    end)
  end

  @doc """
  对 Recipe 进行深度遍历。
  func 会被应用到树中的每一个 Step 上。
  如果 Step 是嵌套的 (NestedStep)，会自动递归进入其内部的 recipe。
  """
  @spec walk(QyCore.Recipe.t(), (Step.t() -> boolean())) :: QyCore.Recipe.t()
  def walk(%__MODULE__{steps: steps} = recipe, func) when is_function(func, 1) do
    new_steps =
      Enum.map(steps, fn step ->
        modified_step = func.(step)

        process_nested(modified_step, func)
      end)

    %{recipe | steps: new_steps}
  end

  defp process_nested(step, func) do
    {impl, in_k, out_k, opts} = ensure_full_step(step)

    if is_atom(impl) and function_exported?(impl, :nested?, 0) and impl.nested?() do
      case Keyword.get(opts, :recipe) do
        %__MODULE__{} = inner_recipe ->
          new_inner_recipe = walk(inner_recipe, func)

          new_opts = Keyword.put(opts, :recipe, new_inner_recipe)
          {impl, in_k, out_k, new_opts}

        _ ->
          step
      end
    else
      step
    end
  end

  defp do_match(step, selector) when is_atom(selector) or is_function(selector, 2) do
    {impl, _in_k, _out_k, _current_opts} = ensure_full_step(step)

    case selector do
      :all -> true
      # 匹配模块
      ^impl -> true
      _ -> false
    end
  end

  defp do_match(step, selector) when is_function(selector, 1) do
    selector.(step)
  end
end
